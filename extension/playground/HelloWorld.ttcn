module HelloWorld {

//====================== Port Types ======================

type port MYPORT message {
    inout charstring
} with {extension "internal"}

//====================== Component Types ======================

type component MYCOMP {
    port MYPORT myport
}

//====================== Constants ======================

const charstring ws0 := "(\n| )#(0,)"

//====================== Templates ======================

// Strings containing the words "hello" and "world", in all small, or
// all capital letters, or small letters with first letter capital;
// exclamation mark is optional; whitespaces allowed
template charstring t_expected :=
    pattern "{ws0}(((h|H)ello {ws0}(w|W)orld)|HELLO {ws0}WORLD){ws0}!#(0,1){ws0}"

//====================== Functions ======================

function f_PTC() runs on MYCOMP {
    alt {
        [] myport.receive(t_expected) { setverdict(pass,"expected message received: ", t_expected) }
        [] myport.receive { setverdict(fail,"not the expected message received: ", t_expected) }
    }
}

//====================== Testcases ======================

testcase TC(charstring pl_toSend) runs on MYCOMP {
    var MYCOMP vl_PTC := MYCOMP.create;
    connect (self:myport, vl_PTC:myport); vl_PTC.start(f_PTC());
    myport.send(pl_toSend); vl_PTC.done
}

//=========================================================================

// Control Part

//=========================================================================

control {
    var charstring vl_inputs [6] :=
        {"HELLO WORLD!","hello world","Hello World!","hello WORD!","hELLO wORLD!","helloworld!"}

    var integer i, vl_noStrings := sizeof(vl_inputs);
    for (i:=0; i< vl_noStrings; i:=i+1) {
        execute (TC(vl_inputs[i]))
    }

} // end of control

} // end of module
